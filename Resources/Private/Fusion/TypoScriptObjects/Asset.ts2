prototype(Shel.MediaFrontend:Asset) < prototype(Content) {
    templatePath = 'resource://Shel.MediaFrontend/Private/Templates/TypoScriptObjects/Asset.html'
    sectionName = 'Asset'

    asset = ${asset}
    # Only show collections in the frontend which are also in the facet
    assetCollections = ${node.properties.selectableCollections ? MediaFrontend.intersectAssetCollections(node.properties.selectableCollections, asset.assetCollections) : asset.assetCollections}

    @context.mediaType = ${asset.mediaType}
    icon = Neos.Fusion:Tag {
        tagName = 'i'
        attributes {
            title = ${mediaType}
            class = Neos.Fusion:Case {
                image {
                    condition = ${String.startsWith(mediaType, 'image')}
                    renderer = 'fa fa-file-image-o'
                }

                video {
                    condition = ${String.startsWith(mediaType, 'video')}
                    renderer = 'fa fa-file-video-o'
                }

                audio {
                    condition = ${String.startsWith(mediaType, 'audio')}
                    renderer = 'fa fa-file-audio-o'
                }

                pdf {
                    condition = ${String.endsWith(mediaType, 'pdf')}
                    renderer = 'fa fa-file-pdf-o'
                }

                default {
                    condition = TRUE
                    renderer = 'fa fa-file-o'
                    @position = 'end'
                }
            }
        }
    }

    @cache {
        mode = 'cached'
        maximumLifetime = '120'
        entryIdentifier {
            assetId = ${asset.identifier}
        }
    }
}
